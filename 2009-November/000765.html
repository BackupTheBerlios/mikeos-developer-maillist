<!DOCTYPE HTML PUBLIC "-//W3C//DTD HTML 4.01 Transitional//EN">
<HTML>
 <HEAD>
   <TITLE> [mikeos-developer] More robust DIR
   </TITLE>
   <LINK REL="Index" HREF="http://lists.berlios.de/pipermail/mikeos-developer/2009-November/index.html" >
   <LINK REL="made" HREF="mailto:mikeos-developer%40lists.berlios.de?Subject=Re%3A%20%5Bmikeos-developer%5D%20More%20robust%20DIR&In-Reply-To=%3C3EF41EFD0D544136BB74BFAE316AEF5C%40turbopumper%3E">
   <META NAME="robots" CONTENT="index,nofollow">
   <style type="text/css">
       pre {
           white-space: pre-wrap;       /* css-2.1, curent FF, Opera, Safari */
           }
   </style>
   <META http-equiv="Content-Type" content="text/html; charset=us-ascii">
   <LINK REL="Previous"  HREF="000763.html">
   <LINK REL="Next"  HREF="000766.html">
 </HEAD>
 <BODY BGCOLOR="#ffffff">
   <H1>[mikeos-developer] More robust DIR</H1>
    <B>Gene Henexson</B> 
    <A HREF="mailto:mikeos-developer%40lists.berlios.de?Subject=Re%3A%20%5Bmikeos-developer%5D%20More%20robust%20DIR&In-Reply-To=%3C3EF41EFD0D544136BB74BFAE316AEF5C%40turbopumper%3E"
       TITLE="[mikeos-developer] More robust DIR">gene.henexson at dobsonteleco.com
       </A><BR>
    <I>Mon Nov 23 05:06:10 CET 2009</I>
    <P><UL>
        <LI>Previous message: <A HREF="000763.html">[mikeos-developer] libmikeos-4.0 has released
</A></li>
        <LI>Next message: <A HREF="000766.html">[mikeos-developer] SlimFast
</A></li>
         <LI> <B>Messages sorted by:</B> 
              <a href="date.html#765">[ date ]</a>
              <a href="thread.html#765">[ thread ]</a>
              <a href="subject.html#765">[ subject ]</a>
              <a href="author.html#765">[ author ]</a>
         </LI>
       </UL>
    <HR>  
<!--beginarticle-->
<PRE>Hey Mike,

Here's yet another DIR and changes to companion routine that although
a bit bulkier will avoid a wrap back to zero if a file size ever busts the
64k threshold. It uses dword for file sizes &amp; total now so capacity
is 20 files,(more in line with file name buffer already in place),
this should make it fairly bubba-proof.

As always, tested on the latest version...didn't break anything.
Enjoy
G


; Use this in place of old list_dir procedure 
;
; ------------------------------------------------------------------

list_directory:               
 mov cx, 0                ; counter

     mov ax, dirlist          ; get list of files on disk
     call os_get_file_list  ; returns location of (20 dword)buffer of sizes in ax

 mov bx, ax
     mov si, dirlist
 mov ah, 0Eh   ; BIOS teletype function

.repeat:
 lodsb    ; Start printing filenames
 cmp al, 0   ; almost done if end of string
 je .lastone

 cmp al, ','   ; If comma in list string, don't print it
 jne .nonewline
 mov cx, si   ; save position in string

.lastone:
 push ax
 call os_get_cursor_pos   ; get current screen position
     mov dl, 16               ; move over to print file size
     call os_move_cursor

     mov ax, word [bx]  ; lsw of file size
 add [tot_size], ax  ; add to lsw of total
 mov dx, word [bx+2]  ; msw of file size
 adc [tot_size+2], dx  ; add to msw of total

 push bx
 mov bx, 10
 mov di, tmp_string
     call os_long_int_to_string    ; convert file size
     mov si, di

     call os_print_string     ; print it
 call os_print_newline
 pop bx
 pop ax
 cmp al, 0   ; finished?
 je .done
  
 add bx, 4   ; advance a dword
 mov si, cx   ; restore list position
 jmp .repeat

.nonewline:
 int 10h
 jmp .repeat

.done:
 mov si, tot_string  ; print '          Total '
 call os_print_string

 mov ax, word [tot_size]
 mov dx, word [tot_size+2]
 mov bx, 10
 mov di, tmp_string
     call os_long_int_to_string
     mov si, di

     call os_print_string     ; print sum of sizes
 call os_print_newline
 mov word [tot_size], 0
 mov word [tot_size+2], 0 ; reset for next DIR

 jmp get_cmd


; ------------------------------------------------------------------
;
;
; ------------------------------------------------------------------
; Changes to declarations are marked ********
;
 input   times 255 db 0
 dirlist   times 255 db 0
 tmp_string  times 15 db 0
 file_size  times 2 dw 0 ;********
 tot_size  times 2 dw 0 ;********

 bin_extension  db 'BIN', 0
 bas_extension  db 'BAS', 0

 prompt   db '&gt; ', 0
 help_text  db 'Inbuilt commands: DIR, CAT, CLS, HELP, TIME, DATE, VER, EXIT', 13, 10, 0
 invalid_msg  db 'No such command or program', 13, 10, 0
 nofilename_msg  db 'No filename specified', 13, 10, 0
 notfound_msg  db 'File not found', 13, 10, 0
 version_msg  db 'MikeOS ', MIKEOS_VER, 13, 10, 0

 exit_string  db 'EXIT', 0
 help_string  db 'HELP', 0
 cls_string  db 'CLS', 0
 dir_string  db 'DIR', 0
 time_string  db 'TIME', 0
 date_string  db 'DATE', 0
 ver_string  db 'VER', 0
 cat_string  db 'CAT', 0


 tot_string  db '          Total ', 0 ;********

 kern_file_string db 'KERNEL.BIN', 0
 kern_warn_msg  db 'Cannot execute kernel file!', 13, 10, 0


; ------------------------------------------------------------------

;
; Changes marked with ****************
; ------------------------------------------------------------------
; os_get_file_list -- Generate comma-separated string of files on floppy &amp; return loc of buffer with sizes 
; IN: AX = location to store zero-terminated filename string
; OUT: AX = location of 20 dword buffer

os_get_file_list:
 pusha

 mov word [.file_list_tmp], ax

 mov eax, 0   ; Needed for some older BIOSes

 call disk_reset_floppy  ; Just in case disk was changed

 mov ax, 19   ; Root dir starts at logical sector 19
 call disk_convert_l2hts

 mov si, disk_buffer  ; ES:BX should point to our buffer
 mov bx, si

 mov ah, 2   ; Params for int 13h: read floppy sectors
 mov al, 14   ; And read 14 of them

 pusha    ; Prepare to enter loop


.read_root_dir:
 popa
 pusha

 stc
 int 13h    ; Read sectors
 call disk_reset_floppy  ; Check we've read them OK
 jnc .show_dir_init  ; No errors, continue

 call disk_reset_floppy  ; Error = reset controller and try again
 jnc .read_root_dir
 jmp .done   ; Double error, exit 'dir' routine

.show_dir_init:
 popa

 mov ax, 0
 mov si, disk_buffer  ; Data reader from start of filenames

 mov word di, [.file_list_tmp] ; Name destination buffer
 mov bx, .fs_buf   ; start of filesize buffer **************** 

.start_entry:
 mov al, [si+11]   ; File attributes for entry
 cmp al, 0Fh   ; Windows marker, skip it
 je .skip

 test al, 10h   ; Is this a directory entry?
 jnz .skip   ; Yes, ignore it

 mov al, [si]
 cmp al, 229   ; If we read 229 = deleted filename
 je .skip

 cmp al, 0   ; 1st byte = entry never used
 je .done


 mov cx, 1   ; Set char counter
 mov dx, si   ; Beginning of possible entry

.testdirentry:
 inc si
 mov al, [si]   ; Test for most unusable characters
 cmp al, ' '   ; Windows sometimes puts 0 (UTF-8) or 0FFh
 jl .nxtdirentry
 cmp al, '~'
 ja .nxtdirentry

 inc cx
 cmp cx, 11   ; Done 11 char filename?
 je .gotfilename
 jmp .testdirentry


.gotfilename:    ; Got a filename that passes testing
 mov si, dx   ; DX = where getting string
 mov ax, word [si+28]  ; read lsw of file size ****************
 mov word [bx], ax  ; put in buffer ****************
 mov ax, word [si+30]  ; read msw of file size ****************
 mov word [bx+2], ax  ; put in buffer ****************
 add bx, 4   ; advance a dword ****************
 mov cx, 0
.loopy:
 mov byte al, [si]
 cmp al, ' '
 je .ignore_space
 mov byte [di], al
 inc si
 inc di
 inc cx
 cmp cx, 8
 je .add_dot
 cmp cx, 11
 je .done_copy
 jmp .loopy

.ignore_space:
 inc si
 inc cx
 cmp cx, 8
 je .add_dot
 jmp .loopy

.add_dot:
 mov byte [di], '.'
 inc di
 jmp .loopy

.done_copy:
 mov byte [di], ','  ; Use comma to separate filenames
 inc di

.nxtdirentry: 
 mov si, dx   ; Start of entry, pretend to skip to next

.skip:
 add si, 32   ; Shift to next 32 bytes (next filename)
 jmp .start_entry


.done:
 dec di
 mov byte [di], 0  ; Zero-terminate string (gets rid of final comma)

 popa
 mov ax, .fs_buf   ; return loc of filesize buffer ****************
 ret


 .file_list_tmp  dw 0
 .fs_buf        times 40 dw 0 ; room for 20 long integers ****************

</PRE>

<!--endarticle-->
    <HR>
    <P><UL>
        <!--threads-->
	<LI>Previous message: <A HREF="000763.html">[mikeos-developer] libmikeos-4.0 has released
</A></li>
	<LI>Next message: <A HREF="000766.html">[mikeos-developer] SlimFast
</A></li>
         <LI> <B>Messages sorted by:</B> 
              <a href="date.html#765">[ date ]</a>
              <a href="thread.html#765">[ thread ]</a>
              <a href="subject.html#765">[ subject ]</a>
              <a href="author.html#765">[ author ]</a>
         </LI>
       </UL>

<hr>
<a href="https://lists.berlios.de/mailman/listinfo/mikeos-developer">More information about the mikeos-developer
mailing list</a><br>
</body></html>
